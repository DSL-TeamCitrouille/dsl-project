grammar DamDam

entry Damier:
   'game' name=ID '{'

       // COMPILE-TIME: Game structure and rules (what the game IS)
       board=Board
       pieces=Pieces
       (dice=Dice)?
       rules=Rules
       objective=Objective
       
       // UI/SKIN: Visual appearance (how the game LOOKS)
       (ui=UI)?
       
       // RUN-TIME: User experience settings (how the game FEELS)
       (settings=Settings)?
   '}';


// ===== COMPILE-TIME (Game Structure) =====
Board:
   'board' '{'
       'size' size=INT
   '}';


Pieces:
   'pieces' '{'
       (piece+=Piece)*
   '}';


Piece:
   'piece' name=ID '{'
       'color' color=ID
       'quantity' quantity=INT
       ('canPromote' canPromote=BOOLEAN)?
   '}';


Dice:
   'dice' '{'
       'faces' faces=INT
       ('quantity' quantity=INT)?
   '}';


Rules:
   'rules' '{'
       (rule+=Rule)*
   '}';


Rule:
   MoveRule | DiceRule | CaptureRule | ActionRule ;


MoveRule:
   'movement' '{'
       'alternating' between+=ID ('and' between+=ID)*
       ('firstPlayer' firstPlayer=ID)?
       ('direction' direction=('diagonal' | 'orthogonal' | 'any'))
       ('canMoveBackward' canMoveBackward=BOOLEAN)?
       ('mandatoryCapture' mandatoryCapture=BOOLEAN)?
       ('exceptWhen' exception=STRING)?
   '}';


DiceRule:
   'dice' '{'
       'diceRolls' rolls=INT
   '}';


CaptureRule:
   'capture' '{'
       'allowed' allowed=BOOLEAN
       ('mandatory' mandatory=BOOLEAN)?
       ('action' action=STRING)?
       ('message' message=STRING)?
   '}';


ActionRule:
   'action' '{'
       'type' type=('move' | 'capture' | 'both')
   '}';


Objective:
   'objective' '{'
       (goal+=Goal)*
   '}';


Goal:
   WinByCapture | WinBySolitaire | WinByForfeit;


WinByCapture:
   'winByCapture' '{'
       'captureAll' target=ID
   '}';


WinBySolitaire:
   'winBySolitaire' '{'
       'removeOwn' removeOwn=BOOLEAN
       'noMoreMoves' movesLeft=INT
   '}';


WinByForfeit:
   'winByForfeit' '{'
       'allowed' allowed=BOOLEAN
   '}';


// ===== UI/SKIN (Visual Appearance) =====
UI:
   'ui' '{'
       (theme=Theme)?
       (sprites=Sprites)?
       (layout=Layout)?
   '}';

Theme:
   'theme' '{'
       ('lightSquares' ':' lightSquares=STRING)?
       ('darkSquares' ':' darkSquares=STRING)?
       ('highlight' ':' highlight=STRING)?
   '}';

Sprites:
   'sprites' '{'
       (sprite+=SpriteDef)*
   '}';

SpriteDef:
   key=ID ':' path=STRING;

Layout:
   'layout' '{'
       ('showCaptured' showCaptured=BOOLEAN)?
   '}';


// ===== RUN-TIME (User Experience) =====
Settings:
   'settings' '{'
       ('animationSpeed' ':' animationSpeed=INT)?
       ('showLegalMoves' ':' showLegalMoves=BOOLEAN)?
       ('aiDifficulty' ':' aiDifficulty=INT)?
   '}';


// ===== TERMINALS =====
terminal INT returns number: /[0-9]+/;
terminal BOOLEAN returns boolean: /true|false/;
terminal STRING: /"[^"]*"|'[^']*'/;
terminal ID: /[a-zA-Z_][a-zA-Z0-9_]*/;


hidden terminal WS: /\s+/;
hidden terminal ML_COMMENT: /\/\*[\s\S]*?\*\//;
hidden terminal SL_COMMENT: /\/\/[^\n\r]*/;